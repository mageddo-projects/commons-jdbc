plugins {
  id 'java-library'
  id 'net.researchgate.release' version '3.0.2'
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"

}

apply from: "${rootDir}/nexus.gradle"

repositories {
  mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.6'
  compileOnly 'org.projectlombok:lombok:1.18.24'
  annotationProcessor 'org.projectlombok:lombok:1.18.24'

  testCompileOnly 'org.projectlombok:lombok:1.18.24'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

testing {
  suites {
    // Configure the built-in test suite
    test {
      // Use JUnit Jupiter test framework
      useJUnitJupiter('5.8.2')
    }
  }
}


release {

  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch.set("master")
  }
  failOnCommitNeeded = false
  failOnPublishNeeded = false
  failOnUnversionedFiles = false
  buildTasks = []
}

confirmReleaseVersion.doLast {
  def f = file("${project.projectDir}/README.md")
  def text = f.text
  f.withWriter { w ->
    w << text.replaceAll("(:\\ )*\\d+\\.\\d+\\.\\d+", "\$1${version}")
  }
}
